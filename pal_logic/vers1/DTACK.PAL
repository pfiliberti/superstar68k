PAL16R4                                           PAL DESIGN SPECIFICATION
DTACK                                             PETER FILIBERTI 11/5/87
68000 CPU DTACK GENERATOR
APPLIED COMPUTER TECHNOLOGY, INC.
CLK /RAM /VID0 /VID1 /VID2 /AS /LDS /UDS DAC GND
/OE /VDTA /DTA /VID /DACS /Q0 /Q1 /RAMH /RAML VCC


Q0    := /Q0 * AS

Q1    := /Q0 * Q1 * AS
      +  Q0 * /Q1 * AS

DACS  := /VID0 * /VID1 * /VID2               ;IF NOT VIDEO DISABLE DAC STROBE
       + /DAC                                ;IF NOT DAC ACCESS DISABLE
       + /AS                                 ;IF /AS HIGH DISBLE
       + Q0                                  ;IF Q0 HIGH DIABLE
       + Q1                                  ;IF Q1 HIGH DISABLE

VID   := VID0
      +  VID1
      +  VID2

IF (VCC) RAML = RAM * LDS * AS                                  ;RAM L SEL

IF (VCC) RAMH = RAM * UDS * AS                                  ;RAM H SEL

IF (VCC) DTA = Q0 * /Q1 * /VID * AS                             ;ROM & I/O
             + Q0 * /Q1 * /VID * RAML                           ;RAM
             + Q0 * /Q1 * /VID * RAMH                           ;RAM
             + Q0 * /Q1 * VID * DAC * AS                        ;VIDEO DAC
             + VID * VDTA * /DAC * AS                           ;VIDEO PROC
             + DTA * AS                                         ;HOLD FUNCTION


FUNCTION TABLE

/OE CLK /RAM /VID0 /VID1 /VID2 /VID /AS /LDS /UDS DAC /VDTA Q1 Q0 /DTA /RAMH
/RAML /DACS
;          - - -                                       - -
;        - V V V -                                     R R
;        R I I I V                                     A A
;        A D D D I                          STATE      M M
;/OE CLK M 0 1 2 D /AS /LDS /UDS  DAC /VDTA Q1 Q0 /DTA H L  /DACS      COMMENT
----------------------------------------------------------------------------
  L   C  H H H H H  H    X    X    L    H   L  L   H   H H    L  STATE RESET
  L   C  H H H H H  L    X    X    H    H   L  H   L   H H    L  ROM /IO FETCH
  L   C  H H H H H  L    X    X    L    H   H  L   L   H H    L  DTACK STATE
  L   C  H H H H H  L    X    X    H    H   H  H   L   H H    L  HOLD FUNCTION
  L   C  H H H H H  H    X    X    L    H   L  L   H   H H    L  END FETCH
  L   C  H H H H H  H    X    X    H    H   L  L   H   H H    L  STATE RESET
  L   C  L H H H H  L    L    H    L    H   L  H   L   H L    L  RAM FETCH
  L   C  L H H H H  L    H    L    H    H   H  L   L   L H    L  DTACK STATE
  L   C  L H H H H  L    L    H    L    H   H  H   L   H L    L  HOLD FUNCTION
  L   C  H H H H H  H    X    X    H    H   L  L   H   H H    L  END FETCH
  L   C  H H H H H  H    X    X    L    H   L  L   H   H H    L  STATE RESET
  L   C  H L H H L  L    X    X    H    H   L  H   L   H H    H  VIDEO DAC I/O
  L   C  H H L H L  L    X    X    H    H   H  L   L   H H    L  DTACK STATE
  L   C  H H H L L  L    X    X    H    H   H  H   L   H H    L  HOLD FUNCTION
  L   C  H H H L L  H    X    X    H    H   L  L   H   H H    L  END FETCH
  L   C  H H H H H  H    X    X    L    H   L  L   H   H H    L  STATE RESET
  L   C  H L H H L  L    X    X    L    H   L  H   H   H H    L  VIDEO FETCH
  L   C  H H L H L  L    X    X    L    H   H  L   H   H H    L  WAIT STATE
  L   C  H H H L L  L    X    X    L    H   H  H   H   H H    L  WAIT STATE
  L   C  H H H L L  L    X    X    L    H   L  L   H   H H    L  WAIT STATE
  L   C  H H H L L  L    X    X    L    L   L  H   L   H H    L  DTACK STATE
  L   C  H H H H H  H    X    X    L    H   L  L   H   H H    L  END FETCH
----------------------------------------------------------------------------


DESCRIPTION

THIS PAL GENERATES THE DTACK SIGNAL FOR THE 68000. IF THE /RAM INPUT AND
/VIDX INPUTS ARE HI THEN A ROM ACCESS IS ASSUMED AND A DTACK IS GENERATED
AFTER 1 CLOCK COUNTS OR 140 NS AFTER AS FALLS. IF ANY /VIDX IS LOW THEN A
VIDEO ACCESS IS ASSUMED AND DTACK GOES LOW WHEN THE /VDTA INPUT GOES LOW.
IF /RAM IS LOW, THEN A RAM ACCESS IS ASSUMED AND THE DTACK IS GENERATED
1 CLOCK COUNT LATER OR 1400 NS LATER. THE /DACS OUTPUT IS LOW WHEN /AS IS HI
AND GOES HI ONE CLOCK CYCLE AFTER /AS GOES LOW.
